dnl Process this file with autoconf to produce a configure script.
dnl $Id: configure.in 617 2004-02-19 08:16:39Z wistaria $

dnl 
dnl Initialization
dnl

dnl disable saving cache
define([AC_CACHE_SAVE], )dnl

AC_PREREQ(2.57)
AC_INIT([looper], 3.0b4)              dnl check here for release!
looper_date="2003/11/20"                dnl check here for release!

AC_PREFIX_DEFAULT([$HOME/ALPS])

AC_DEFINE_UNQUOTED(LOOPER_VERSION, "$PACKAGE_VERSION")
AC_DEFINE_UNQUOTED(LOOPER_DATE, "$looper_date")

AC_MSG_NOTICE([starting configuration for ALPS/looper version $PACKAGE_VERSION ($looper_date)])

dnl
dnl check for ALPS library
dnl

AC_MSG_CHECKING([for ALPS library])
if test -f "$ac_cv_alps_include_mk"; then
  AC_MSG_RESULT([OK])
else
  AC_MSG_RESULT([not found])
  AC_MSG_ERROR([ALPS library not found.])
fi
AC_SUBST(INCLUDE_MK)
INCLUDE_MK="$ac_cv_alps_include_mk"

AC_MSG_CHECKING([for ALPS version])
AC_MSG_RESULT([$ac_cv_alps_version])

dnl
dnl check for LAPACK
dnl

sinclude(config/ac_lapack.m4)
AC_LAPACK
AC_SUBST(TESTOK_LAPACK)
if test "$ac_cv_have_lapack" = yes; then
  TESTOK_LAPACK='$(TESTOK_LAPACK)'
fi

dnl
dnl check for MPI library
dnl

AC_MSG_CHECKING([for MPI library])
AC_SUBST(PROGRAM_MPI)
AC_SUBST(EXAMPLE_MPI)
if test "$ac_cv_alps_have_mpi" = yes; then
  PROGRAM_MPI='$(PROGRAM_MPI)'
  EXAMPLE_MPI='$(EXAMPLE_MPI)'
  AC_MSG_RESULT([yes])
else
  AC_MSG_RESULT([no])
fi

dnl
dnl check for PVM library
dnl

AC_MSG_CHECKING([for PVM library])
AC_SUBST(PROGRAM_PVM)
AC_SUBST(EXAMPLE_PVM)
if test "$ac_cv_alps_have_pvm" = yes; then
  PROGRAM_PVM='$(PROGRAM_PVM)'
  EXAMPLE_PVM='$(EXAMPLE_PVM)'
  AC_MSG_RESULT([yes])
else
  AC_MSG_RESULT([no])
fi

dnl
dnl generate files
dnl

AC_CONFIG_HEADERS(looper/config.h)
AC_CONFIG_FILES(include-looper.mk)
AC_CONFIG_FILES(Makefile test/Makefile)
AC_CONFIG_FILES(lattices.xml models.xml)
AC_CONFIG_FILES(percolation-params qmc-params)
AC_OUTPUT
